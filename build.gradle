buildscript {
   repositories {
      mavenCentral()
   }
   dependencies {
    classpath 'com.github.townsfolk:gradle-release:1.2'
   }
}

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'release'

group = 'de.felixschulze.gradle'

ext.archivesBaseName = 'gradle-spoon-plugin'
ext.isSnapshot = version.endsWith("-SNAPSHOT")

repositories {
    mavenCentral()
	maven {
        url "https://raw.github.com/Scout24-CoC-MPS/AS24-Android-Maven-Repo/master"
    }
}

dependencies {
    groovy localGroovy()
    compile gradleApi()
    compile 'com.squareup.spoon:spoon-runner:1.0.6-as1'
    compile 'com.android.tools.build:gradle:0.5.+'
}

task sourceJar(type: Jar) {
    description = 'An archive of the source code for Maven Central'
    classifier = 'sources'
    from sourceSets.main.groovy
}

task groovydocJar(type: Jar) {
    description = 'An archive of the GroovyDocs for Maven Central'
    classifier = 'javadoc'
    from groovydoc
}

artifacts {
    archives groovydocJar, sourceJar
}

signing {
    sign configurations.archives
}

// Only *require* signing if we are uploading a non snapshot version
gradle.taskGraph.whenReady { taskGraph ->
    tasks.withType(org.gradle.plugins.signing.Sign).all {
        required = taskGraph.hasTask(":uploadArchives") && !isSnapshot
    }
}


install.repositories.mavenInstaller {
    pom.project(pomConfiguration)
    if (signing.signatory) {
        beforeDeployment { signing.signPom(it) }
    }
}

uploadArchives {
    repositories.mavenDeployer {
        if (signing.signatory) {
            beforeDeployment { signPom(it) }
        }
        name = 'mavenCentralReleaseDeployer'
        repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
            authentication(userName: System.properties['sonatypeUsername'], password: System.properties['sonatypePassword'])
            releases(updatePolicy: 'always')
            snapshots(updatePolicy: 'always')
        }
        pom.project(pomConfiguration)
    }
}

release {
	createReleaseTag.dependsOn uploadArchives
}


/**v
 * Create POM config and return for use by other tasks.
 */
def getPomConfiguration() {
    return {
        name 'Gradle Spoon Plugin'
        packaging 'jar'
        description 'A Gradle plugin for running Android instrumentation tests with Spoon.'
        url 'https://github.com/x2on/gradle-spoon-plugin'
        licenses {
            license {
                name 'The MIT License'
                url 'http://www.opensource.org/licenses/mit-license.php'
                distribution 'repo'
            }
        }
        developers {
            developer {
                id 'x2on'
                name 'Felix Schulze'
                email 'code@felixschulze.de'
            }
        }
        scm {
            connection 'scm:https://x2on@github.com/x2on/gradle-spoon-plugin'
            developerConnection 'scm:git@github.com:x2on/gradle-spoon-plugin.git'
            url 'https://github.com/x2on/gradle-spoon-plugin'
        }
    }
}